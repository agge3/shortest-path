plugins {
    id 'jacoco'
}

dependencies {
    testImplementation 'org.mockito:mockito-core:3.4.6'

    compileOnly project(":lib:MessageBus")
}

group = 'shortestpath'
version = '1.17.17'

test {
    jvmArgs = ['-Xms1g', '-Xmx8g']
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file('reports/jacoco/test/jacocoTestReport.xml')
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
}

// Convenience task to start the RuneLite client with the ShortestPathPlugin
tasks.register('runelite', JavaExec) {
    group = 'application'
    description = 'Runs shortestpath.ShortestPathPluginTest with assertions enabled'
    classpath = sourceSets.test.runtimeClasspath
    mainClass.set('shortestpath.ShortestPathPluginTest')
    jvmArgs '-ea'
    args '--developer-mode'
}
